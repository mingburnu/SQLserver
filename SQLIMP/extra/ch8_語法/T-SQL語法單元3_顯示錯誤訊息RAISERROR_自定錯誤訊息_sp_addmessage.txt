1 RAISERROR : 顯示錯誤訊息資訊
2 sp_addmessage 自訂錯誤訊息

-----------------------------------------------------------------
RAISERROR : 顯示錯誤訊息資訊

定義
 RAISERROR 也可用來傳回訊息。相較於 PRINT，RAISERROR 有下列優點：
 RAISERROR 支援利用以 C 語言標準程式庫 printf 函數為基礎來建立型模的機制，將引數代入錯誤訊息中。
 除了文字訊息之外，RAISERROR 還可以指定唯一錯誤號碼、嚴重性和狀態碼。
 RAISERROR 可用來傳回利用 sp_addmessage 系統預存程序建立的使用者自訂訊息。

語法
RAISERROR ( { msg_id | msg_str | @local_variable }
           { ,severity ,state }
           [ ,argument [ ,...n ] ] )
          [ WITH option [ ,...n ] ]

註  state 介於 0 到 255 之間的整數。負值或大於 255 的值會產生錯誤。
    severity 使用者自訂嚴重性層級。任何使用者皆可指定從 0 到 18 的嚴重性層級。從 19 到 25 的嚴重性層級只
             能由系統管理員 (sysadmin) 固定伺服器角色成員或具有 ALTER TRACE 權限的使用者指定。


---------------------------------------------------------------------------------------
例1
RAISERROR ('錯誤訊息!' ,  10,    1  ); 

--RAISERROR 與 PRINT 功能類似 都在訊息視窗; SELECT 在結果視窗

RAISERROR ('顯示訊息1!' ,  0,    1  ); 
PRINT      '顯示訊息2!'
SELECT     '顯示訊息3!'



---------------------------------------------------------------------------------------
例2
RAISERROR (N'This is message %s %d.', -- 訊息內容Message text.
           10,                        -- 嚴重性Severity,
           1,                         -- 狀態State,
           N'number',                 -- 第一參數First argument.
           5);                        -- 第二參數Second argument.

-- The message text returned is: This is message number 5.
GO

---------------------------------------------------------------------------------------
例3

A. 從 CATCH 區塊傳回錯誤資訊 不work 在 try就錯不讓執行到 catch
下列程式碼範例顯示如何在 TRY 區塊內使用 RAISERROR，使執行位置跳到相關聯的 CATCH 區塊。
這個範例也會顯示如何利用 RAISERROR，來傳回叫用 CATCH 區塊之錯誤的相關資訊。


BEGIN TRY
    -- RAISERROR with severity 11-19 will cause execution to 
    -- jump to the CATCH block.
    RAISERROR ('Error raised in TRY block.', -- Message text.
               19, -- Severity.
               1 -- State.
               );
END TRY
BEGIN CATCH
    DECLARE @ErrorMessage NVARCHAR(4000);
    DECLARE @ErrorSeverity INT;
    DECLARE @ErrorState INT;

    SELECT 
        @ErrorMessage = ERROR_MESSAGE(),
        @ErrorSeverity = ERROR_SEVERITY(),
        @ErrorState = ERROR_STATE();

    -- Use RAISERROR inside the CATCH block to return error
    -- information about the original error that caused
    -- execution to jump to the CATCH block.
    RAISERROR (@ErrorMessage, -- Message text.
               @ErrorSeverity, -- Severity.
               @ErrorState -- State.
               );
END CATCH;

--錯誤訊息
訊息 50000，層級 16，狀態 1，行 22
Error raised in TRY block.

---------------------------------------------------------------------------------------
sp_addmessage 自訂錯誤訊息:

在 sys.messages 中建立特定訊息

語法
 
sp_addmessage [ @msgnum = ] msg_id , [ @severity = ] severity , [ @msgtext = ] 'msg' 
     [ , [ @lang = ] 'language' ] 
     [ , [ @with_log = ] { 'TRUE' | 'FALSE' } ] 
     [ , [ @replace = ] 'replace' ] 

參數 
  [@msgnum = ] msg_id : 使用者自訂錯誤訊息的 msg_id 可以是 50,001 和 2,147,483,647 之間的整數。


下列範例會顯示如何引發儲存在 [sys.messages] 目錄檢視的訊息。使用 sp_addmessage 系統預存程序，
將訊息編號為 50005 的訊息加入至 [sys.messages] 目錄檢視中。

---------------------------------------------------------------------------------------
例 5-1 自訂錯誤訊息 - 中英兩種版本

--須先設成英文版本 訊息 15279，層級 16，狀態 1，程序 sp_addmessage，行 97
--您必須先加入這個訊息的 us_english 版本才能加入 '繁體中文' 版本。

例 1
EXEC sp_addmessage 66666, 7, 'Update Complete!',  @lang = 'us_english'
GO
RAISERROR (66666, 7, 1)

EXEC sp_addmessage 66666, 7, '更新完成!',  @lang = '繁體中文', @replace = 'replace';
GO

RAISERROR (66666, 7, 1)    --錯誤代碼, 嚴重層級, 狀態

exec sp_dropmessage @msgnum = 66666;  --刪除自訂訊息

---------------------------------------------------------------------------------------
例 5-2 
set language us_english
go
exec sp_addmessage @msgnum = 50005,
              @severity = 10,
              @msgtext = N'Error msg!',
              @replace = 'replace' ;
go
exec sp_addmessage @msgnum = 50005,
              @severity = 10,
              @msgtext = N'錯誤訊息!',
              @lang = '繁體中文',
              @replace = 'replace' ;


GO
RAISERROR (50005, -- Message id.
           10,    -- Severity
           1      -- State
           ); 
GO
set language  繁體中文  --切換成中文訊息
GO
RAISERROR (50005, -- Message id.
           10,    -- Severity
           1      -- State
           ); 
GO

exec sp_dropmessage @msgnum = 50005;
GO
set language  繁體中文

----------------------------------------------------